apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 33
    ndkVersion "25.2.9519653"

    defaultConfig {
        applicationId "at.tomtasche.reader"
        minSdkVersion 21
        targetSdkVersion 33

        testApplicationId "at.tomtasche.reader.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        vectorDrawables.useSupportLibrary true

        externalNativeBuild.cmake.targets "odr-core"
        externalNativeBuild.cmake.arguments "-DANDROID_STL=c++_shared"
    }

    flavorDimensions "default"

    signingConfigs {
        releasePro {
            storeFile file("../../google_play.keystore")
            storePassword "releaseme"
            keyAlias "reader-pro"
            keyPassword "releaseme"
        }

        releaseLite {
            storeFile file("../../google_play.keystore")
            storePassword "releaseme"
            keyAlias "reader"
            keyPassword "releaseme"
        }
    }

    productFlavors {
        lite {
            signingConfig signingConfigs.releaseLite

            resValue("bool", "DISABLE_TRACKING", "false")
        }

        pro {
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"

            signingConfig signingConfigs.releasePro

            resValue("bool", "DISABLE_TRACKING", "true")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'

            firebaseCrashlytics.nativeSymbolUploadEnabled true
            ndk.debugSymbolLevel = "full"
        }
    }

    android.bundle.language.enableSplit false

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            version "3.22.0+"
            path file('../CMakeLists.txt')
        }
    }
    lint {
        abortOnError false
    }
    packagingOptions {
        jniLibs {
            pickFirsts += ['lib/armeabi-v7a/libtmpfile.so', 'lib/arm64-v8a/libtmpfile.so', 'lib/x86/libtmpfile.so', 'lib/x86_64/libtmpfile.so', 'lib/armeabi-v7a/libc++_shared.so', 'lib/arm64-v8a/libc++_shared.so', 'lib/x86/libc++_shared.so', 'lib/x86_64/libc++_shared.so']
        }
    }
    namespace 'at.tomtasche.reader'
}

dependencies {
    implementation platform("org.jetbrains.kotlin:kotlin-bom:1.8.10")

    implementation platform('com.google.firebase:firebase-bom:32.2.0')
    implementation 'com.google.firebase:firebase-storage'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-crashlytics-ndk'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-config'

    implementation 'com.google.android.gms:play-services-ads:22.2.0'
    implementation 'com.google.android.play:core:1.10.3'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core:1.10.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.webkit:webkit:1.7.0'

    implementation 'com.viliussutkus89:pdf2htmlex-android:0.18.19'
    implementation 'com.viliussutkus89:wvware-android:1.2.8'
    implementation 'com.github.huzongyao:AndroidMagic:v1.1.2'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    // espresso-idling-resource is used in main sourceSet as well. cannot be just androidTestImplementation
    implementation 'androidx.test.espresso:espresso-idling-resource:3.5.1'
    implementation 'androidx.annotation:annotation:1.6.0'
}