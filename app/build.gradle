apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    ndkVersion "22.0.7026061"

    defaultConfig {
        applicationId "at.tomtasche.reader"
        minSdkVersion 16
        targetSdkVersion 30

        testApplicationId "at.tomtasche.reader.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        vectorDrawables.useSupportLibrary true

        externalNativeBuild.cmake.targets "odr-core"
    }

    dexOptions.javaMaxHeapSize "4g"

    flavorDimensions "default"

    signingConfigs {
        releasePro {
            storeFile file("../../google_play.keystore")
            storePassword "releaseme"
            keyAlias "reader-pro"
            keyPassword "releaseme"
        }

        releaseLite {
            storeFile file("../../google_play.keystore")
            storePassword "releaseme"
            keyAlias "reader"
            keyPassword "releaseme"
        }
    }

    productFlavors {
        lite {
            signingConfig signingConfigs.releaseLite
        }

        pro {
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"

            signingConfig signingConfigs.releasePro
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'

            firebaseCrashlytics.nativeSymbolUploadEnabled true
            ndk.debugSymbolLevel = "full"
        }
    }

    android.bundle.language.enableSplit false

    lintOptions.abortOnError false

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            version "3.12.4+"
            path file('../CMakeLists.txt')
        }
    }
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:26.0.0')
    implementation 'com.google.firebase:firebase-storage'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-ads'
    implementation 'com.google.firebase:firebase-perf'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-crashlytics-ndk'

    implementation 'com.android.billingclient:billing:3.0.2'

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core:1.3.2'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.webkit:webkit:1.4.0'

    implementation 'com.github.AppIntro:AppIntro:5.1.0'

    implementation('io.github.kobakei:ratethisapp:1.2.0') {
        exclude group: 'com.google.android'
        exclude group: 'com.android.support'
    }

    implementation('com.nononsenseapps:filepicker:4.2.1', {
        exclude group: 'com.android.support'
    })

    implementation 'com.viliussutkus89:pdf2htmlex-android:0.18.10'
    implementation 'com.viliussutkus89:wvware-android:1.2.4'
    implementation 'com.github.huzongyao:AndroidMagic:v1.1.2'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
}

apply plugin: 'com.google.gms.google-services'
